@Metadata.layer: #CORE
@Search: { searchable: true }
@UI.headerInfo: { typeName:       'Project',
                  typeNamePlural: 'Projects',
                  title:           { value: 'ProjectName' },    
                  description:     { value: 'ProjectUUID' } }   
                  
annotate view ZBUV_C_ProjectDraft_TP
    with 
{
@UI.facet: [ 
     
   { purpose        : #HEADER,
     position       : 10,
     targetQualifier: 'StatusData',
     type           : #DATAPOINT_REFERENCE},     
          
   { position : 10,
     label    : 'General Information',
     id       : 'GeneralInfo',   
     type     : #COLLECTION },

        { parentId : 'GeneralInfo',
          position : 10,
          label    : 'General',
          purpose  : #STANDARD,
          type     : #IDENTIFICATION_REFERENCE },         
          
   { position : 20,
     label    : 'Tasks',
     id       : 'Tasks', 
     purpose  : #STANDARD,  
     type     : #LINEITEM_REFERENCE,
     targetElement: '_Task_TPD' }    ]
    
    @UI.lineItem      : [{ type: #FOR_ACTION, dataAction: 'BOPF:COMPLETE_PROJECT', label: 'Complete Project' }]
    @UI.identification: [{ type: #FOR_ACTION, dataAction: 'BOPF:COMPLETE_PROJECT', label: 'Complete Project' }] 
    @UI.hidden: true
    ProjectUUID;
    
    @EndUserText.label: 'Project ID'
    @UI.lineItem      : [{ position: 10, importance: #HIGH, type: #STANDARD }]
    @UI.identification: [{ position: 10 }]     
    ProjectID;

    @EndUserText.label: 'Project Name'
    @UI.lineItem      : [{ position: 20, importance: #HIGH  }]
    @UI.identification: [{ position: 20 }]
    @Search.defaultSearchElement: true
    @Search.fuzzinessThreshold: 0.7     
    ProjectName;
    
    @EndUserText.label: 'Estimation'
    @UI.lineItem      : [{ position: 30, importance: #HIGH, type:#AS_DATAPOINT  }]
    @UI.identification: [{ position: 30 } ]  
    @UI.dataPoint     : { targetValue: 5, visualization:#RATING }
    ProjectEstimation;
    
    @EndUserText.label: 'Project Status'
    @UI.lineItem      : [{ position: 40, importance: #HIGH }]--, criticality: 'StatusCriticality' }]
    @UI.selectionField: [{ position: 10 }]
    @UI.identification: [{ position: 40 }]
    @UI.dataPoint     : { qualifier: 'StatusData',
                          title: 'Status'}--,
                          --criticality: 'StatusCriticality' }  
    @UI.textArrangement: #TEXT_ONLY                        
    @Consumption.valueHelpDefinition: [{ entity: { name    : 'ZBUV_I_Status', 
                                                   element : 'StatusID'}  }] 

    ProjectStatus;
    
}